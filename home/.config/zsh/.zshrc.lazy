#------------------------------
# zstyle
#------------------------------

# 補完で小文字を大文字にマッチさせる
# zstyle ':completion:*' matcher-list '' 'm:{a-z}={A-Z}' 'm:{a-z}{A-Z}={A-Za-z}'
zstyle ':completion:*' matcher-list '' 'm:{a-z}={A-Z}'

# 補完メニューを選択できるようにする
zstyle ':completion:*:default' menu select=2

# 補完候補をグループ化
zstyle ':completion:*' group-name ''

# 補完候補の説明表示
zstyle ':completion:*' format '%B%F{12}%d%f%b'
zstyle ':completion:*:warnings' format '%F{11}%BNo matches for:%b %d%f'

# 補完候補に色をつける
# zstyle ':completion:*:default' list-colors ""
zstyle ':completion:*' list-colors 'di=01;34' 'ln=01;36' 'so=01;35' 'ex=01;32' 'bd=40;33;01' 'cd=40;33;01'

# 補完候補の指定
# _expand: グロブや変数の展開を行う。
# _oldlist: 前回の補完結果を再利用する。
# _complete: 普通の補完関数
# _match: globを展開しないで候補の一覧から補完する。
# _history: ヒストリのコマンドも補完候補とする。
# _ignored: 補完候補に出さないと指定したものも補完候補とする。
# _approximate: 似ている補完候補も補完候補とする。
# _prefix: カーソル以降を無視してカーソル位置までで補完する。
zstyle ':completion:*' completer _expand _oldlist _complete _match _approximate _prefix

# 補完キャッシュを使う
zstyle ':completion:*' use-cache true
zstyle ':completion:*' cache-path "${XDG_CACHE_HOME}/zsh/.zcompcache"

# おせっかいな補完候補を表示する
zstyle ':completion:*' verbose yes

# ../ の後は今いるディレクトリを補完しない
zstyle ':completion:*' ignore-parents parent pwd ..

# sudo の後のコマンド名補完
zstyle ':completion:*:sudo:*' command-path /bin /sbin /usr/bin /usr/sbin /usr/local/bin /usr/local/sbin /opt/bin

# プロセス名補完
zstyle ':completion:*:processes' command 'ps x -o pid,s,args'

# 補完してほしくないファイルは補完しない
zstyle ':completion:*:*:nvim:*:*files' ignored-patterns '*?.out' '*?.o' '*?.hi' '*?~' '*\#'

# オプション表示時のseparator
# add --> add paths to the index
zstyle ':completion:*' list-separator '-->'

# 何も入力されていないときのTabでTabが挿入されるのを抑制
# zstyle ':completion:*' insert-tab false

# 単語区切り文字
zstyle ':zle:*' word-chars ' /@#$%^&*-+=;:{}[]()<>,.|\\'
zstyle ':zle:*' word-style unspecified
## スラッシュを除く
WORDCHARS=${WORDCHARS//[\/]}

# [&|]を入力時に、補完によって挿入されたスペースを消さない
ZLE_SPACE_SUFFIX_CHARS=$'&|'

#------------------------------
# alias
#------------------------------
type exa >/dev/null 2>&1 && alias ls='exa'
alias grep='\grep --color=auto'

# sudo でaliasが使えるようにする
alias sudo='sudo '

# alias history='\history -f -D -i'

# set wallpaper
alias wallpaper='feh --no-fehbg --randomize --bg-max --recursive'

# xrandr
alias x-reset-mode='xrandr --output $(xrandr | grep '\''connected primary'\'' | awk '\''{print $1}'\'') --mode $(xrandr | grep '\'' +'\'' | head -1 | awk '\''{print $1}'\'')'

alias chino='clang++ -std=c++2b -Weverything -Wno-c++98-compat-pedantic -Wno-c11-extensions -pedantic-errors -I./include -O2 -pipe'
alias atcoder-cc='clang++ -std=c++20 -Weverything -Wno-c++98-compat-pedantic -Wno-c11-extensions -Wno-unused-macros -Wno-unused-const-variable -pedantic-errors -g -O0 -pipe -DLOCAL -DDEBUG'

# alias fcrontab='fcrontab -i'

alias note=' ${EDITOR} -c "cd %:h" "${HOME}/Dropbox/note/note.md"'
alias nvimrc=' ${EDITOR} -c "cd %:h" "${XDG_CONFIG_HOME}/nvim/init.vim"'
alias zshrc=' ${EDITOR} -c "cd %:h" "${XDG_CONFIG_HOME}/zsh/.zshrc"'
alias bashrc=' ${EDITOR} -c "cd %:h" "${HOME}.bashrc"'
alias fishconfig=' ${EDITOR} -c "cd %:h" "${XDG_CONFIG_HOME}/fish/config.fish"'
alias i3config=' ${EDITOR} -c "cd %:h" "${XDG_CONFIG_HOME}/i3/config"'
alias gitconfig=' git config --global -e'

# alias encrypt='openssl aes-256-cbc -e -iter 100'
# alias decrypt='openssl aes-256-cbc -d -iter 100'

alias history-edit=' ${EDITOR} "${HISTFILE}"'
alias history-sort='fc -l -n 0 | sort'

# typo
#alias exho=echo
# alias :q=exit


#------------------------------
# global alias
#------------------------------
# alias -g GREP='| grep'
# alias -g SED='| sed'
# alias -g COPY='| pbcopy'


#------------------------------
# suffix alias
#------------------------------
alias -s {mp3,flac,m4a}=music-play
alias -s hs=runhaskell
alias -s c=my-runc
alias -s {cpp,cxx,cc}=runchino
alias -s ml=ocaml
alias -s html=open
alias -s {tgz,tbz2,gz,bz2,xz}=extract
alias -s tar='tar xvf'
alias -s zip='unzip'
alias -s rar='unrar x'
alias -s jar='java -jar'
# alias -s encrypted='decrypt-edit'
alias -s ts='deno run --allow-all --unstable'


#------------------------------
# compile function
#------------------------------
alias my-cc='clang -std=c11 -Wall -Wextra -pedantic-errors -O2 -pipe'

#my-cxx ()
#{
#  clang++ -std=c++1z -Weverything -Wno-c++98-compat -Wno-c++98-compat-pedantic -pedantic-errors -O2 -stdlib=libc++ -I../include -Iinclude "$@" -lc++abi
#}

my-runc ()
{
  my-cc -o /tmp/a.out "$@" && /tmp/a.out
}


runchino ()
{
  chino -o /tmp/a.out "$@" && printf "\033[1;32mcompile succeded.\033[0m\n" >&2 && /tmp/a.out
}

atcoder-run ()
{
  [[ "$1" -nt "./a.out" ]] && atcoder-cc -o ./a.out "$1"
  shift
  ./a.out "$@"
}

ojt ()
{
  [[ $# -eq 0 ]] && {
    set -- *.cpp
    if [[ $# -gt 1 ]] && {
      set -- "$(printf "%s\n" "$@" | fzf --preview 'bat --color=always --style=numbers --line-range :500 {}')"
    }
    if [[ $# -eq 0 ]] && {
      printf "no source file\n" >&2
      return 1
    }
  }
  atcoder-cli bash -c 'g++ -std=c++2b -Wall -Wextra -pedantic-errors -O2 -pipe -DLOCAL -DDEBUG -o ./a.out '"$@"' && oj t'
}

# sandbox用(?)
my-ghc ()
{
  if [[ -e .cabal-sandbox ]]
  then
    ghc -package-db .cabal-sandbox/*.conf.d "$@"
  else
    ghc "$@"
  fi
}


#------------------------------
# debug function
#------------------------------
valgrind_cpp ()
{
  atcoder-cc -o ./a.out "$1" && valgrind ./a.out
}


#------------------------------
# utility function
#------------------------------
# ぐぐる
google ()
{
  [[ -z $* ]] && set -- `head -1` && open "https://www.google.com/#q=$*"
}

# ほぐる
hoogle ()
{
  [[ -z $* ]] && set -- `head -1` && open "https://www.haskell.org/hoogle/?hoogle=$*"
}

# stderrへのecho、ついでに終了コード1
echoerr ()
{
  echo "$@" >&2
  return 1
}
eprintf ()
{
  printf "$@" >&2
}

# ♪
music-play ()
{
  mplayer "$@" || echoerr Error: cannot play.
}

git-repositories ()
{
  if type fd > /dev/null 2>&1
  then
    fd --hidden --no-ignore --type d --exclude .cache --glob .git --format {//}
  else
    find . -type d -name .git -exec dirname {} \;
  fi
}

# ~以下のすべてのリポジトリに対してgit statusを実行
# .cacheは除外
git-status-all ()
{
  local i
  # find . -name .cache -prune -o -name .git -type d -exec dirname {} \; | while read i
  git-repositories | while read i
  do
    printf "%s\n" "$i"
    \git -C "$i" status -s
  done
}

color-palette ()
{
  local c
  for c in {0..255}
  do
    printf "\e[38;5;%sm %03d" $c $c
    [[ $(($c%16)) -eq 15 ]] && printf "\n"
  done
}

powerlinefont-test ()
{
  local c
  for c in {$((0xe0a0))..$((0xe0bf))}
  do
    # printf "%s\n" $c
    printf "U+%04X %b" "$c" "$(printf '\\u%04x' $c)"
    [[ $(($c%4)) -eq 3 ]] && printf "\n" || printf "\t"
  done
}

ripgrep-replace ()
{
  if [[ $# -lt 2 ]]
  then
    cat << EOS
Usage: ripgrep-replace [rg options...] <old> <new>
EOS
    return 1
  fi

  local new="${@[-1]}"
  local old="${@[-2]}"
  local rg_opts=("${@:1:-2}")
  # printf "Debug: new=%s\n" "$new"
  # printf "Debug: old=%s\n" "$old"
  # printf "Debug: args=%s\n" "$@"
  # printf "Debug: \$#=%s\n" "$#"
  # printf "Debug: \$#-1=%s\n" "$(($#-1))"
  # printf "Debug: ???=%s\n" "${@:1:1}"
  # printf "Debug: rg_opts=%s\n" "${rg_opts[@]}"
  # return 0

  # おそらく安全な区切り文字
  local sep=$'\x1f'

  # printf "debug: %s\n" "s${sep}${old}${sep}${new}${sep}g"
  rg --hidden --no-ignore --glob '!.git' "${rg_opts[@]}" -l -0 "$old" | xargs -0 sed -i "s${sep}${old}${sep}${new}${sep}g"
}

#------------------------------
# nvim-remote
#------------------------------
# nvim in nvim terminalにならないようにする
# $EDITORを変えるとコミットメッセージの編集時とかにも親のnvimを使ってしまう(それは違う)ので、$EDITORを変えずにnvimを変える
[[ -n $NVIM ]] && nvim ()
{
  if [[ -z $* ]]
  then
    command nvim --headless --server $NVIM --remote-send "<Cmd>hide<CR>"
  else
    # command nvim --server $NVIM --remote "$@"
    command nvim --headless --server $NVIM --remote-tab "$@"
  fi
}
# [[ -n $NVIM ]] && alias nvim=__nvim-remote




#------------------------------
# fzf
#------------------------------
export FZF_DEFAULT_COMMAND='fd --type f --hidden --no-ignore --exclude .git'

fzf-file ()
{
  fd --type f --hidden --no-ignore --exclude .git "$@" | fzf --multi --preview 'bat --color=always --style=numbers --line-range :500 {}'
}
fzf-directory ()
{
  fd --type d --hidden --no-ignore --exclude .git "$@" | fzf --multi --preview 'exa -lha --time-style long-iso --color=always {}'
}
fzf-process ()
{
  ps -ef | sed 1d | fzf --multi | awk '{print $2}'
}
fzf-git-branch ()
{
  git branch --all | cut -c 3- | fzf --reverse -0 --height 60%
}
fzf-git-commit ()
{
  git log --graph --all --oneline --color=always | fzf --ansi --no-sort --reverse --tiebreak index -0 --height=60% --preview "git show --color=always \$(printf '%s' {} | grep -io '[0-9a-f]\{7,\}' | head -1)" | \grep -io '[0-9a-f]\{7,\}' | head -1
}
fzf-git-changed ()
{
  git diff --name-only --relative | fzf --multi --preview 'git diff --color=always -- {}'
}
fzf-git-repository ()
{
  git-repositories | fzf --multi --preview 'git -c color.status=always -C {} status'
}


#------------------------------
# subsh
#------------------------------
__precmd_for_subsh ()
{
  [[ -n "${SUBSH}" ]] && print -z "${SUBSH} "
}

add-zsh-hook precmd "__precmd_for_subsh"

subsh ()
{
  export SUBSH="$*"
}


#------------------------------
# keybind
#------------------------------
# reset
# bindkey -d

# Emacs風キーバインド
bindkey -e

function __expand-or-complete-prefix-trim-end()
{
  local last_cursor=$#LBUFFER
  [[ $last_cursor -eq 0 ]] && return 0
  zle expand-or-complete-prefix
  ## 文字が追加された場合のみ
  if [[ $last_cursor -lt $#LBUFFER ]]
  then
    ## 末尾がスペースじゃないときは、代入自体を避けないとスラッシュの自動補完の情報が消えてしまう
    case "${LBUFFER}" in
      *\ ) LBUFFER="${LBUFFER% }" ;;
    esac
  fi
}
zle -N __expand-or-complete-prefix-trim-end
bindkey "^I" __expand-or-complete-prefix-trim-end

# Up, Down
# いい感じの履歴補完
autoload -Uz history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
  ## Arch Linux
  bindkey "^[[A" history-beginning-search-backward-end
  bindkey "^[[B" history-beginning-search-forward-end

  ## Ubuntu
  bindkey "^[OA" history-beginning-search-backward-end
  bindkey "^[OB" history-beginning-search-forward-end

# Ctrl+arrow key
  ## Ubuntu
  bindkey "^[[1;5C" forward-word
  bindkey "^[[1;5D" backward-word

# Shift+arrow key
  ## Arch Linux
  bindkey -s "^[[1;2A" ""
  bindkey -s "^[[1;2B" ""
  bindkey "^[[1;2C" forward-word
  bindkey "^[[1;2D" backward-word

# Alt+arrow key
  bindkey -s "^[[1;3A" ""
  bindkey -s "^[[1;3B" ""
  bindkey "^[[1;3C" forward-char
  bindkey "^[[1;3D" backward-char

# HOME,ENDで移動する
  ## Arch Linux
  bindkey "^[[H" beginning-of-line
  bindkey "^[[F" end-of-line
  ## Ubuntu
  bindkey "^[OH" beginning-of-line
  bindkey "^[OF" end-of-line
  ## ?
  # bindkey "^[[1~" beginning-of-line
  # bindkey "^[[4~" end-of-line

# Deleteキーで消す
bindkey "^[[3~" delete-char

# Ctrl+Deleteでカーソル位置の単語を消す
bindkey "^[[3;5~" delete-word

# Ctrl+BackSpace
  ## Ubuntu
  bindkey "^H" backward-kill-word

# Shift+Tabで逆順補完
bindkey "^[[Z" reverse-menu-complete

# Ctrl-P, Ctrl-Nで履歴を遡る
bindkey "^P" history-beginning-search-backward
bindkey "^N" history-beginning-search-forward

# 勝手にquotで囲むな
bindkey -r "^['"
bindkey -r "^[\""

__fzf_history()
{
  local res
  res=$(fc -l -n -r 0 | fzf --query "$LBUFFER") || {
    zle reset-prompt
    return 1
  }
  LBUFFER="$res"
  zle reset-prompt
}
zle -N __fzf_history
bindkey "^R" __fzf_history

# Esc+keyを無効化する(experimental)
bindkey -s "^[" ""
KEYTIMEOUT=1

# abbreviation
type zabbrev >/dev/null 2>&1 && eval "$(zabbrev init --bind-keys)"


#------------------------------
# 環境依存
#------------------------------
case $(uname -r) in
  *Microsoft)
    # WSL1
    export DISPLAY=localhost:0

    if [[ -e /etc/wslstart.sh ]]
    then
      (wsl.exe --user root --exec /etc/wslstart.sh &)
    else
      printf "Error in %s: \"%s\": no such file or directory\n" "$0" "/etc/wslstart.sh" >&2
    fi

    alias pbcopy=clip.exe
    alias pbpaste='powershell.exe get-clipboard | iconv -f cp932 | sed -z '\''s/\r\n/\n/g'\'
    alias open='powershell.exe start'

    alias explorer=explorer.exe
    alias java=java.exe
    alias ffmpeg=ffmpeg.exe
    alias ffprobe=ffprobe.exe
    alias ffplay=ffplay.exe

    alias -s bat='cmd.exe /c'
    alias -s ps1='powershell.exe -NoLogo -NoProfile -ExecutionPolicy Bypass -File'
    ;;
  *microsoft*)
    # WSL2
    ;;
  *)
    case ${OSTYPE} in
      darwin*)
        # Mac
        # alias ls='\ls -G -F'
        ;;
      linux*)
        # linux
        # alias ls='\ls --color=auto -F'
        alias pbcopy='xsel --clipboard --input'
        alias pbpaste='xsel --clipboard --output'
        alias open=xdg-open

        type wine > /dev/null 2>&1 && alias -s exe='wine'
        ;;
    esac
    ;;
esac

# [[ -d /usr/share/fzf ]] && {
#   source /usr/share/fzf/completion.zsh
# }


#------------------------------
# autoload
#------------------------------

# 先方予測機能
#autoload -Uz predict-on && predict-on

# 単語の区切り文字を設定
autoload -Uz select-word-style && select-word-style default

# run-help
alias run-help > /dev/null 2>&1 && unalias run-help
autoload -Uz run-help run-help-git run-help-openssl run-help-sudo

# 補完を有効にする
autoload -Uz compinit && compinit
# autoload -Uz _zinit
# (( ${+_comps} )) && _comps[zinit]=_zinit
# zpcompinit
